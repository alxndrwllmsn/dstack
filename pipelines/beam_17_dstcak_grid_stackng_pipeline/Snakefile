"""This is an example Snakemake makefile to
run some grid stacking without slurm support so far
"""

#=== Define variables for the pipeline ===
#Absolute path to working directory
WORKING_DIR = os.getcwd() #YandaSoft needs absolute paths

CHANNELS = 11

#This maps the irregularry names input MS to a regular naming scheme
MS_MAPPING = {'night_1':'scienceData_SB10991_G23_T0_B_06.beam17_SL_C_100_110',
            'night_2':'scienceData_SB11000_G23_T0_B_03.beam17_SL_C_100_110'}

#=== Define rules ===
#ruleorder: create_deep_parset > deep_imaging #only needed for rules writing the same files e.g. not in this pipeline

#Master rule:
rule all:
    input:
        'test/stacked_grids/image.deep.restored'

#Other pipeline rules:
rule create_parset:
    input:
        MS =  lambda wildcards: 'measurement_sets/{0:s}.ms'.format(MS_MAPPING[wildcards.night_index]),
        template_parset = 'template_parset.in'
    output:
        'test/{night_index}/dumpgrid_parset.in'
    params:
        op = 'test/{night_index}', #Setup a variable using a wildcard
        working_dir = WORKING_DIR
    shell:
        'dparset -i Cimager -n image.dumpgrid -g WProject -op {params.op} \
-pn dumpgrid_parset.in -t {input.template_parset} -tn dstack -a dataset={params.working_dir}/{input.MS}'

rule first_pass_imaging:
    input:
       'test/{night_index}/dumpgrid_parset.in'
    output:
        directory('test/{night_index}/{night_index}.grid')
    log:
        'test/{night_index}/logfile_dumpgrid_{night_index}.log' #We can set variables inside log e.g: log: log1: 'a.log', log2: 'b.log' then in shell call {log.log1}
    shell:
        'echo mpirun -np {0:d}'.format(CHANNELS+1) + ' imager -c {input} > {log}; mkdir {output}' #This is how to mix wildcards and variables in the shell execution string

rule grid_stacking:
    input:
        ['test/{0:s}/{0:s}.grid'.format(night) for night in MS_MAPPING.keys()] #We define the relgular naming scheme used in our mapping for the input MS!
    output:
        directory('test/stacked_grids/grid.deep'),
        directory('test/stacked_grids/psfgrid.deep'),
        directory('test/stacked_grids/pcf.deep')
    params:
        cp = 'test/stacked_grid',
        cn = 'deep.grid'
    shell:
        'echo dstacking -cl {input} -cp {params.cp} -cn {params.cn} -c; mkdir {output}'

rule create_deep_parset:
    input:
        grid = 'test/stacked_grids/grid.deep',
        psfgrid = 'test/stacked_grids/psfgrid.deep',
        pcf = 'test/stacked_grids/pcf.deep',
        template_parset = 'template_parset.in'
    output:
        'test/stacked_grids/cdeconvolver_{image_names}.in' #Need to have a wildcard in the output so I can pass it to a variable
    params:
        image_names = '{image_names}' #If I don't want to hardcode it in case I want to scale upt to beams
    shell:
        'dparset -i Cdeconvolver -n {params.image_names} -g WProject -op test/stacked_grids \
-pn cdeconvolver_{params.image_names}.in -t {input.template_parset} -tn dstack -a grid={input.grid} \
psfgrid={input.psfgrid} pcf={input.pcf}'

rule deep_imaging:
    input:
        grid = 'test/stacked_grids/grid.deep',
        parset = 'test/stacked_grids/cdeconvolver_{image_names}.in'
    output:
        directory('test/stacked_grids/{image_names}.restored')
    log:
        'test/stacked_grids/logfile_cdeconvolver_{image_names}.log' #Logfile have to contain the same wildcards as all output
    shell:
        'echo mpirun -np {0:d}'.format(CHANNELS) + ' cdeconvolver-mpi -c {input.parset} > {log}; mkdir {output}'